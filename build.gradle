plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.+" apply false // Quiltflower, a better decompiler
    id "io.github.p03w.machete" version "1.+" // automatic jar compressing on build
    id "org.jetbrains.kotlin.jvm" version "1.7.21" apply false
}

// Determine the version
if (project.hasProperty("CustomReleaseVersion")) {
    // Remove release/ from the version if present
    version = project.property("CustomReleaseVersion").replaceFirst("^release/", "")
} else {
    String gitRevision = "git rev-parse HEAD".execute().text.trim()

//    version = mod_version + '+' + gitRevision.substring(0, 10)
    version = mod_version + '+' + gitRevision
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"
    apply plugin: "org.jetbrains.kotlin.jvm"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        if (!block_external_repositories) {
            mavenCentral()
            maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
            maven { url = "https://dvs1.progwml6.com/files/maven/" } // JEI
            maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
            maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
            maven {
                name = 'Ladysnake Mods'
                url = 'https://ladysnake.jfrog.io/artifactory/mods'
            }
            maven { // Flywheel
                url = "https://maven.tterrag.com/"
                content {
                    // need to be specific here due to version overlaps
                    includeGroup("com.jozufozu.flywheel")
                }
            }
            maven {
                url = "https://maven.realrobotix.me/createbigcannons/" // Create Big Cannons
                content {
                    includeGroup "com.rbasamoyai"
                }
            }
        }
    }

    if (project.block_external_repositories) {
        repositories.removeIf {
            def url = it.url.toString()
            url.contains("maven.minecraftforge.net") ||
                    url.contains("maven.fabricmc.net") ||
                    url.contains("maven.architectury.dev")
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // layered mappings - Mojmap names, parchment and QM docs and parameters
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${qm_version}:intermediary-v2")
            it.parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
            it.officialMojangMappings { nameSyntheticMembers = false }
        })

        // see each subproject for dependencies.
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    group = rootProject.maven_group

    // Formats the mod version to include the loader, Minecraft version, and build number (if present)
    // example: 1.0.0+fabric-1.18.2-100
    String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
    version = "${mod_version}+${project.name}-${minecraft_version}" + (buildNumber != null ? "-${buildNumber}" : "")

    repositories {
        mavenLocal()
        maven {
            name = "Valkyrien Skies Internal"
            url = project.vs_maven_url ?: 'https://maven.valkyrienskies.org'
            if (project.vs_maven_username && project.vs_maven_password) {
                credentials {
                    username = project.vs_maven_username
                    password = project.vs_maven_password
                }
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
    }

    machete {
        // disable machete locally for faster builds
        enabled = buildNumber != null
    }
}
